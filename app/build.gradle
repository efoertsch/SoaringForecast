apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    //buildToolsVersion "25.0.2" - Android Gradle Plugin now includes default version of build tools
    defaultConfig {
        applicationId "org.soaringforecast.rasp"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 8
        versionName "1.5"
        //https://artemzin.com/blog/how-to-mock-dependencies-in-unit-integration-and-functional-tests-dagger-robolectric-instrumentation/
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //testInstrumentationRunner "org.soaringforecast.rasp.app.OverrideApplicationTestRunner"

        // to use any vector drawables
        vectorDrawables.useSupportLibrary = true

        // to export room db schema - good for db validation and migration validation/checking
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    // To resolve error - DuplicateFileException: Duplicate files copied in APK META-INF/rxjava.properties
    // caused by using RxJava v1 w/ Retrofit v2
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


}

// to pick up mock xml responses
//http://stackoverflow.com/questions/37221590/how-do-i-access-a-text-file-for-junit-test-in-android
// to share test classes/files
//http://blog.danlew.net/2015/11/02/sharing-code-between-unit-tests-and-instrumentation-tests-on-android/
android.sourceSets {
    String sharedTestJava = 'src/sharedTest/java'
    String sharedTestResources = 'src/sharedTest/resources'
    test {
        java.srcDirs += sharedTestJava
        resources.srcDirs += sharedTestResources
    }
    androidTest {
        java.srcDirs += sharedTestJava
        resources.srcDirs += sharedTestResources
    }
}



dependencies {
    final SUPPORT_LIBRARY_VERSION = '28.0.0'
    final CONSTRAINT_LIBRARY_VERSION = '1.1.3'
    final GOOGLE_MAPS_VERSION = '16.0.0'
    final ANDROID_MAPS_UTILS = '0.5+'
    final RX_ANDROID = '2.0.2'
    final RX_JAVA = '2.1.12'
    final RETROFIT = '2.2.0'
    final SIMPLE_XML = '2.1.0'
    final GSON = '2.8.2'
    final GSON_CONVERTER = '2.3.0'
    final GLIDE = '4.8.0'
    final OKHTTP = '3.8.0'
    final DAGGER = '2.15'
    final TIMBER = '4.7.0'
    final ESPRESSO = '2.2.2'
    final HAMCREST = '1.3'
    final RUNNER = '1.0.1'
    final UIAUTOMATOR = '2.1.3'
    final JUNIT = '4.12'
    final MOCKITO = '2.4.0'
    final EVENT_BUS = '3.1.1'
    final MEMORY_CACHE = '1.0.2.Final'
    final DISK_CACHE = '2.0.2'
    final ROOM_VERSION = '1.1.1'
    final ARCH_LIFE_CYCLE_VERSION = '1.1.1'
    final work_version = "1.0.0-alpha10"
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android compat/support libraries
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:preference-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support.constraint:constraint-layout:$CONSTRAINT_LIBRARY_VERSION"
    implementation "com.google.android.gms:play-services-maps:$GOOGLE_MAPS_VERSION"

    // Bus
    implementation "org.greenrobot:eventbus:$EVENT_BUS"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$RETROFIT"
    implementation("com.squareup.retrofit2:adapter-rxjava2:$RETROFIT") {
        exclude group: 'io.reactivex.java2', module: 'rxjava'
    }

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA"
    implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID"

    // SimpleXML
    implementation("com.squareup.retrofit2:converter-simplexml:$SIMPLE_XML") {
        exclude module: 'stax-api'
        exclude module: 'stax'
        exclude module: 'xpp3'
    }

    // GSON (JSON)
    implementation "com.google.code.gson:gson:$GSON"
    implementation "com.squareup.retrofit2:converter-gson:$GSON_CONVERTER"

    //okhttp
    implementation "com.squareup.okhttp3:okhttp:$OKHTTP"

    //Dagger2
    implementation "com.google.dagger:dagger:$DAGGER"
    implementation "com.google.dagger:dagger-android:$DAGGER"
    implementation "com.google.dagger:dagger-android-support:$DAGGER"
    annotationProcessor "com.google.dagger:dagger-android-processor:$DAGGER"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER"

    // Diskcache
    implementation "com.jakewharton:disklrucache:$DISK_CACHE"

    // Memory cache
    implementation "org.cache2k:cache2k-api:$MEMORY_CACHE"
    implementation "org.cache2k:cache2k-core:$MEMORY_CACHE"

    //Room
    implementation "android.arch.persistence.room:runtime:$ROOM_VERSION"
    implementation "android.arch.persistence.room:rxjava2:$ROOM_VERSION"
    annotationProcessor "android.arch.persistence.room:compiler:$ROOM_VERSION"

    // Glide for Geos satellite gif
    implementation "com.github.bumptech.glide:glide:$GLIDE"
    annotationProcessor "com.github.bumptech.glide:compiler:$GLIDE"

    // For display of SUA GeoJSON and special markers on map
    implementation "com.google.maps.android:android-maps-utils:$ANDROID_MAPS_UTILS"

    // WorkManager
    implementation "android.arch.work:work-runtime:$work_version"

    // Lifecycle components
    implementation "android.arch.lifecycle:extensions:$ARCH_LIFE_CYCLE_VERSION"
    annotationProcessor "android.arch.lifecycle:compiler:$ARCH_LIFE_CYCLE_VERSION"

    // Timber for logging
    implementation "com.jakewharton.timber:timber:$TIMBER"

    // JVM Unit Testing---------------------------------------------------------------------
    testImplementation "junit:junit:$JUNIT"
    testImplementation "org.mockito:mockito-core:$MOCKITO"
    testImplementation "org.powermock:powermock-api-mockito:1.7.0RC2"
    testImplementation "org.powermock:powermock-module-junit4:1.7.0RC2"
    testImplementation "org.powermock:powermock-api-mockito2:1.7.0RC2"

    testImplementation "android.arch.persistence.room:testing:$ROOM_VERSION"

    //Android testing---------------------------------------------------------------------------
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$ESPRESSO") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'javax.inject'
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$ESPRESSO") {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$ESPRESSO") {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestImplementation "com.android.support.test:runner:$RUNNER"
    androidTestImplementation "com.android.support.test:rules:$RUNNER"

    // Test helpers for Room
    androidTestImplementation "android.arch.persistence.room:testing:$ROOM_VERSION"
    androidTestImplementation "android.arch.core:core-testing:$ROOM_VERSION"

    // Matchers
    androidTestImplementation "org.hamcrest:hamcrest-library:$HAMCREST"

    // WorkManager - Test helpers
    androidTestImplementation "android.arch.work:work-testing:$work_version"

    //UI testing with UI Automator
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$UIAUTOMATOR"
}
